apply plugin: 'com.android.application'

apply plugin: 'realm-android'

android {

    def STRING = "String"
    def API_URL = "API_URL"
    def STAGING_URL = "\"its staging url\""
    def DEV_URL = "\"its dev url\""
    def PROD_URL = "\"its release url\""

    compileSdkVersion target_sdk_version
    defaultConfig {
        applicationId "com.geektech.astudy"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField STRING, API_URL, DEV_URL
        }
        staging {
            minifyEnabled false
            buildConfigField STRING, API_URL, STAGING_URL
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField STRING, API_URL, PROD_URL
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "cc_${variant.buildType.name}_${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    //Unit
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'

    //Android
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'


    implementation project(':analytics')

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    //Firebase core
    implementation 'com.google.firebase:firebase-core:16.0.1'

    //Firebase messaging
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    //Dagger
    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'

    //Butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc2'

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.4"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}

apply plugin: 'com.google.gms.google-services'
